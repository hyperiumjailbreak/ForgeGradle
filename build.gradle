buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.gradle.plugin-publish"

group = 'com.hyperiumjailbreak.gradle'
version = '2.1-SNAPSHOT'
archivesBaseName = 'ForgeGradle'
targetCompatibility = '1.8'
sourceCompatibility = '1.8'

repositories {
    mavenLocal()
    maven {
        name = "forge"
        url = "https://files.minecraftforge.net/maven"
    }

    maven {
        // because Srg2Source needs an eclipse dependency.
        name = "eclipse"
        url = "https://repo.eclipse.org/content/groups/eclipse/"
    }
    jcenter() // get as many deps from here as possible
    mavenCentral()

    // because of the GradleStart stuff
    maven {
        name = "mojang"
        url = "https://libraries.minecraft.net/"
    }
}

configurations {
    deployerJars
    shade
    compileOnly.extendsFrom shade
}

dependencies {
    compile gradleApi()

    // moved to the beginning to be the overrider
    compile 'org.ow2.asm:asm-debug-all:5.0.3'
    compile 'com.google.guava:guava:18.0'

    compile 'net.sf.opencsv:opencsv:2.3' // reading CSVs.. also used by SpecialSource
    compile 'com.cloudbees:diff4j:1.1' // for difing and patching
    compile 'com.github.abrarsyed.jastyle:jAstyle:1.3' // formatting

    compile 'com.google.code.gson:gson:2.2.4' // Used instead of Argo for buuilding changelog.
    compile 'com.github.tony19:named-regexp:0.2.3' // 1.7 Named regexp features
    compile 'net.minecraftforge:fernflower:2.0-SNAPSHOT' // Fernflower Forge edition

    shade 'net.md-5:SpecialSource:1.7.4' // deobf and reobs

    // mcp stuff
    shade 'de.oceanlabs.mcp:RetroGuard:3.6.6'
    shade 'de.oceanlabs.mcp:mcinjector:3.4-SNAPSHOT'
    shade 'net.minecraftforge.srg2source:Srg2Source:3.3-SNAPSHOT'

    //Stuff used in the GradleStart classes
    compileOnly 'com.mojang:authlib:1.5.16'
    compileOnly "net.minecraft:launchwrapper:1.11"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

sourceSets {
    main.compileClasspath += configurations.shade;
    main.runtimeClasspath += configurations.shade;
}

compileJava {
	options.deprecation = true
	//options.compilerArgs += ["-Werror"]
	//options.compilerArgs += ["-Werror", "-Xlint:unchecked"]
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }

    manifest {
        attributes 'version': project.version
        attributes 'javaCompliance': project.targetCompatibility
        attributes 'group': project.group
        attributes 'Implementation-Version': project.version + getGitHash()
    }
}

artifacts {
    archives jar
}

pluginBundle {
    website = 'http://www.gradle.org/'
    vcsUrl = 'https://github.com/hyperiumjailbreak/ForgeGradle'
    description = 'Gradle plugin for HyperiumJailbreak'
    tags = ['forge', 'minecraft', 'minecraftforge']

    plugins {
        minecraftTweaker {
            id = 'com.hyperiumjailbreak.minecraft-tweaker'
            displayName = 'Minecraft Client Tweaker Plugin'
        }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: "https://hyperiumjailbreak.mycloudrepo.io/repositories/libs") {
            authentication(userName: "deploy@rdil.rocks", password: "FILLIN")
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
            project {
                name project.archivesBaseName
                packaging 'jar'
                description 'Gradle plugin for Forge'
                url 'https://github.com/hyperiumjailbreak/ForgeGradle'

                scm {
                    url 'https://github.com/hyperiumjailbreak/ForgeGradle'
                    connection 'scm:git:git://github.com/hyperiumjailbreak/ForgeGradle.git'
                    developerConnection 'scm:git:git@github.com:hyperiumjailbreak/ForgeGradle.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/hyperiumjailbreak/ForgeGradle/issues'
                }

                licenses {
                    license {
                        name 'Lesser GNU Public License, Version 2.1'
                        url 'https://www.gnu.org/licenses/lgpl-2.1.html'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

def getGitHash() {
    def process = 'git rev-parse --short HEAD'.execute()
    process.waitFor()
    return '-' + (process.exitValue() ? 'unknown' : process.text.trim())
}
